plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.11-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'd452ef7612'
}

preprocess {
	strictExtraMappings = false

	def mc1211 = createNode('1.21.1', 1_21_01, '')
	def mc1213 = createNode('1.21.3', 1_21_03, '')
	def mc1214 = createNode('1.21.4', 1_21_04, '')
	def mc1215 = createNode('1.21.5', 1_21_05, '')
	def mc1218 = createNode('1.21.8', 1_21_08, '')
	def mc12110 = createNode('1.21.10', 1_21_10, '')

	mc1211.link(mc1213, null)
	mc1213.link(mc1214, null)
	mc1214.link(mc1215, null)
	mc1215.link(mc1218, null)
	mc1218.link(mc12110, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy = DuplicatesStrategy.INCLUDE
			}
		}
	}

}
